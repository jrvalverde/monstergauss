      PROGRAM COMPARE
C
C     PURPOSE
C        COMPARE OUTPUT LISTINGS OF DIFFERENT VERSIONS OF MONSTERGAUSS
C        TEST JOBS - THE OLD SET ARE NAMED 'TESTNN.OUT' AND THE NEW SET
C        ARE NAMED 'TESTNN.OUT.NEW' WHERE 'NN' IS THE 2 DIGIT TEST NUMBER.
C
C     THE PROGRAM READS IN THE LOWER AND UPPER TEST JOB NUMBERS TO BE
C     COMPARED ON TWO SEPARATE INPUT LINES, AND THE THIRD LINE CONTAINS
C     A FLAG ('Y' OR 'N') TO INDICATE WHETHER DIFFERENCES LIKE 'E+' VS
C     'D+' AND '0.' VS ' .' ARE TO BE IGNORED ('Y') OR NOT ('N').
C     LINES WITH ONLY SIGN DIFFERENCES ('-' VS ' ') ARE IGNORED.
C
      LOGICAL SEARCH
C
      CHARACTER L1*134, L2*134, L3*134, F1*20, F2*20, YESNO*1
C
      DATA L1/' '/, L2/' '/, L3/' '/
      DATA SEARCH/.FALSE./
C
C
      WRITE (6,*) 'Enter test number to start at:'
      READ (5,*,END=900) NSTART
      WRITE (6,*) 'Enter test number to end at:'
      READ (5,*,END=900) NEND
      IF (NEND .LT. NSTART) NEND = NSTART
      WRITE (6,*) 'Enter "y" to ignore E+/D+ and leading zeroes:'
      READ (5,110,END=900) YESNO
  110 FORMAT (A1)
      IF (YESNO.EQ.'Y' .OR. YESNO.EQ.'y') SEARCH = .TRUE.
C
C     LOOP OVER ALL THE TEST JOBS TO BE COMPARED.
C
      DO 400 N=NSTART,NEND
C
C     SET THE FILE NAMES TO BE COMPARED.
C
      WRITE (F1,120) N
  120 FORMAT ('test',I2.2,'.out')
      WRITE (F2,130) N
  130 FORMAT ('test',I2.2,'.out.new')
C
C     OPEN THE TWO FILES.
C
      OPEN (UNIT=1, FILE=F1, FORM='FORMATTED',
     1 STATUS='OLD', IOSTAT=ISTAT, ERR=160)
      OPEN (UNIT=2, FILE=F2, FORM='FORMATTED',
     1 STATUS='OLD', IOSTAT=ISTAT, ERR=180)
C
      WRITE (6,150) F1, F2
  150 FORMAT ('1Compare files ',A,' and ',A/)
      NLINE = 1
      NDIFF = 0
      NLASTD = 0
      GO TO 200
C
  160 WRITE (6,170) F1, ISTAT
  170 FORMAT (/' *** OPEN OF FILE ',A,' (UNIT 1) GAVE ISTAT =',I12,'.')
      GO TO 400
C
  180 WRITE (6,190) F2, ISTAT
  190 FORMAT (/' *** OPEN OF FILE ',A,' (UNIT 2) GAVE ISTAT =',I12,'.')
      CLOSE (UNIT=1)
      GO TO 400
C
C     SKIP OVER THE JCL IN EACH FILE (DEFINED AS LINES STARTING WITH '$'.
C
  200 READ (1,210,END=930) L1
  210 FORMAT (A133)
      IF (L1(2:2) .EQ. '$') GO TO 200
C
  220 READ (2,210,END=940) L2
      IF (L2(2:2) .EQ. '$') GO TO 220
C
C     COMPARE THE TWO FILES, STOPPING AT THE FIRST END OF FILE FOUND.
C
C     CONVERT 'D+' AND 'D-' IN FILE 2 TO 'E+' AND 'E-', AND CONVERT
C     LEADING BLANKS TO ZEROES IF FOLLOWED BY A '.', IF SEARCH IS SET.
C
C     CONVERT ' .' TO '-.' AND '-.' TO ' .' IN FILE 2 IF NECESSARY
C     TO MATCH STRINGS DIFFERENT BY SIGN ONLY. NOTE THAT THIS CODE IS
C     NOT ACTIVE NOW BECAUSE THE SYSTEM EMITS LEADING ZEROES ON ALL
C     FRACTIONS WRITTEN WITH F FORMAT.
C     CONVERT ' N.' TO '-N.' AND '-N.' TO ' N.' IN FILE 2 IF NECESSARY
C     TO MATCH STRINGS DIFFERENT BY SIGN ONLY.
C     CONVERT 'N ' TO 'M ' WHERE N (IN FILE 2) AND M (IN FILE 1) DIFFER
C     BY 1 DIGIT IF NECESSARY TO MATCH STRINGS DIFFERENT BY ONLY THE
C     LAST DIGIT OF A REAL NUMBER.
C
  230 DO 240 I=1,132
C
C     CONVERT 'D+' AND 'D-' IN FILE 2 TO 'E+' AND 'E-', AND CONVERT
C     LEADING BLANKS TO ZEROES IF FOLLOWED BY A '.', IF SEARCH IS SET.
C
      IF (SEARCH) THEN
         IF (L1(I:I).EQ.'E' .AND. (L2(I:I).EQ.'D'.OR.L2(I:I).EQ.'d'))
     1    THEN
            IF (L2(I+1:I+1).EQ.'+' .OR. L2(I+1:I+1).EQ.'-')
     1       L2(I:I) = 'E'
         ELSE IF ((L1(I:I+2).EQ.' 0.'.OR.L1(I:I+2).EQ.'-0.') .AND.
     1    (L2(I:I+2).EQ.'  .'.OR.L2(I:I+2).EQ.' -.')) THEN
            L2(I:I) = L2(I+1:I+1)
            L2(I+1:I+1) = '0'
         END IF
      END IF
C
C     CONVERT ' .' TO '-.' AND '-.' TO ' .' IN FILE 2 IF NECESSARY
C     TO MATCH STRINGS DIFFERENT BY SIGN ONLY.
C
C     IF (L1(I+1:I+1).EQ.'.' .AND. L2(I+1:I+1).EQ.'.') THEN
C        IF (L1(I:I).EQ.'-' .AND. L2(I:I).EQ.' ') THEN
C           L2(I:I) = '-'
C        ELSE IF (L1(I:I).EQ.' ' .AND. L2(I:I).EQ.'-') THEN
C           L2(I:I) = ' '
C        END IF
C     END IF
C
C     CONVERT ' N.' TO '-N.' AND '-N.' TO ' N.' IN FILE 2 IF NECESSARY
C     TO MATCH STRINGS DIFFERENT BY SIGN ONLY.
C
      IF (L1(I+1:I+1).EQ.L2(I+1:I+1) .AND.
     1 L1(I+1:I+1).GE.'0' .AND. L1(I+1:I+1).LE.'9' .AND.
     2 L1(I+2:I+2).EQ.'.' .AND. L2(I+2:I+2).EQ.'.') THEN
         IF (L1(I:I).EQ.'-' .AND. L2(I:I).EQ.' ') THEN
            L2(I:I) = '-'
         ELSE IF (L1(I:I).EQ.' ' .AND. L2(I:I).EQ.'-') THEN
            L2(I:I) = ' '
         END IF
      END IF
C
C     CONVERT 'N ' TO 'M ' IN FILE 2 IF NECESSARY TO MATCH STRINGS
C     DIFFERENT BY ONLY THE LAST DIGIT OF A REAL NUMBER.
C
      IF (L1(I:I).NE.L2(I:I) .AND.
     1 L1(I:I).GE.'0' .AND. L1(I:I).LE.'9' .AND.
     2 L2(I:I).GE.'0' .AND. L2(I:I).LE.'9' .AND.
     3 L1(I+1:I+1).EQ.' ' .AND. L2(I+1:I+1).EQ.' ') THEN
         IF (IABS(ICHAR(L1(I:I))-ICHAR(L2(I:I))) .EQ. 1) THEN
            DO 235 J=I-1,1,-1
            IF (L1(J:J) .NE. L2(J:J)) GO TO 240
            IF (L1(J:J).GE.'0' .AND. L1(J:J).LE.'9') GO TO 235
            IF (L1(J:J) .NE. '.') GO TO 240
            L2(I:I) = L1(I:I)
            NLASTD = NLASTD + 1
            GO TO 240
  235       CONTINUE
         END IF
      END IF
  240 CONTINUE
C
C     BLANK OUT WALL-CLOCK TIME ON TIMING MESSAGES.
C
      IF (L1(1:5).EQ.'0*** ' .AND. L2(1:5).EQ.'0*** ' .AND.
     1 L1(8:8).EQ.':' .AND. L2(8:8).EQ.':' .AND.
     2 L1(11:11).EQ.':' .AND. L2(11:11).EQ.':') THEN
         L1(6:13) = 'xx:xx:xx'
         L2(6:13) = 'xx:xx:xx'
      END IF
C
      IF (L1 .EQ. L2) GO TO 300
C
C     IGNORE SIGN DIFFERENCES.
C
      DO 245 I=1,133
      IF (L1(I:I) .EQ. L2(I:I)) GO TO 245
      IF (L1(I:I).EQ.' ' .AND. L2(I:I).EQ.'-') GO TO 245
      IF (L1(I:I).EQ.'-' .AND. L2(I:I).EQ.' ') GO TO 245
      GO TO 246
  245 CONTINUE
      GO TO 300
C
C     PRINT THE DIFFERENT LINES, SUPPRESSING TRAILING BLANKS.
C
  246 DO 250 I=133,2,-1
      LAST1 = I
      IF (L1(I:I) .NE. ' ') GO TO 251
  250 CONTINUE
C
  251 DO 255 I=133,2,-1
      LAST2 = I
      IF (L2(I:I) .NE. ' ') GO TO 256
  255 CONTINUE
C
  256 WRITE (6,260) NLINE, L1(1:1), L2(1:1), L1(2:LAST1), L2(2:LAST2)
  260 FORMAT (/'*** DIFFERENCE AT LINE',I5,', CARRIAGE CONTROL: (',A1,
     1 ') AND (',A1,')'/1X,A/1X,A)
C
      LAST3 = 2
      DO 270 I=2,133
      IF (L1(I:I) .EQ. L2(I:I)) THEN
         L3(I:I) = ' '
      ELSE
         L3(I:I) = '-'
         LAST3 = I
      END IF
  270 CONTINUE
C
      WRITE (6,280) L3(2:LAST3)
  280 FORMAT (1X,A)
      NDIFF = NDIFF + 1
      IF (NDIFF .LE. 200) GO TO 300
C
      WRITE (6,290)
  290 FORMAT (/' *** TOO MANY DIFFERENCES, COMPARISON ABANDONED.')
      GO TO 390
C
C     READ THE NEXT LINES.
C
  300 READ (1,210,END=310) L1
      READ (2,210,END=330) L2
      NLINE = NLINE + 1
      GO TO 230
C
C     END OF FILE ON FILE 1.
C
  310 WRITE (6,320) NLINE,  L2
  320 FORMAT (/' *** EOF ON FILE 1 AT LINE',I5,
     1 ' LAST LINE READ FROM FILE 2:'/A133)
      GO TO 390
C
C     END OF FILE ON FILE 2.
C
  330 WRITE (6,340) NLINE,  L1
  340 FORMAT (/' *** EOF ON FILE 2 AT LINE',I5,
     1 ' LAST LINE READ FROM FILE 1:'/A133)
C
C     CLOSE THE INPUT FILES.
C
  390 CLOSE (UNIT=1)
      CLOSE (UNIT=2)
      IF (NLASTD .NE. 0) WRITE (6,395) NLASTD
  395 FORMAT (/' *** NUMBER OF SINGLE LAST DIGIT DIFFERENCES WAS',I10)
C
  400 CONTINUE
C
      STOP
C
C     ERROR EXITS.
C
  900 WRITE (6,901)
  901 FORMAT (/' *** EOF ON UNIT 5 WHILE READING NSTART/NEND.')
      STOP
C
  930 WRITE (6,931)
  931 FORMAT (/' *** EOF ON UNIT 1 DURING JCL SKIP.')
      STOP
C
  940 WRITE (6,941)
  941 FORMAT (/' *** EOF ON UNIT 2 DURING JCL SKIP.')
      STOP
      END
