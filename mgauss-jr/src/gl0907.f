C     GL0907       04 JUL 87                                         MRP
C?IBM/GLD/GBR/VAX/UNX
      SUBROUTINE OEPMO
C??
C?CDC
C     PROGRAM OEPMO
C??
C=O.E.P. TRANSFORMATION
C CHAPTER 15: ONE ELECTRON PROPERTY TRANSFORMATION.
C ***********
C
C1TRANSFORMATION
C     LINK 0907
C
C     THIS PROGRAM TRANSFORMS THE AO INTEGRALS OF ONE ELECTRON
C     PROPERTIES TO MO INTEGRALS.
C
C     AUTHOR: R. A. POIRIER,
C     DEPARTMENT OF CHEMISTRY,
C     UNIVERSITY OF TORONTO, TORONTO, CANADA.
C*
C     INPUT REQUIREMENTS
C*
C     IUNIT,NPROP,ICODE (3I4)
C*
C     IUNIT IS THE UNIT WHERE THE AO INTEGRALS ARE TO BE READ.
C     NPROP IS THE SEQUENCE NUMBER OF THE PROPERTY ON UNIT IUNIT.
C     ICODE = 0 IF THE SAME ORBITALS ARE TO BE TRANSFORMED AS WAS
C               SPECIFIED BY IGROUP IN THE INTEGRAL TRANSFORMATION
C               (GL0901)
C           = 1 TRANSFORM ALL MO'S (IGNORE IGROUP).
C     *NOTE* IF NO INTEGRAL TRANSFORMATION WAS PERFORMED THEN THE
C               PROGRAM RUNS AS IF ICODE=1.
C
C     REPEAT FOR EACH PROPERTY TRANSFORMATION DESIRED.
C*
C     SET IUNIT TO ZERO TO INDICATE THE END OF THE TRANSFORMATIONS.
C*
C1OPTIONS
C     IOP(20) ... ONE ELECTRON PROPERTIES.
C     0  INTEGRAL TRANSFORMATION
C     1  ATOMIC MULTIPOLE DECOMPOSITION
C==
C
C1
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C#
C     PARAMETER (NA=#NA, NB=#NB)
C     PARAMETER (NT=#NT)
C##
      PARAMETER (NA= 36, NB=200)
      PARAMETER (NT= 70)
C###
      PARAMETER (ZERO=0.0D0)
C
      COMMON /A/ IOP(99),IC1(NA),IC2(6),NBASIS,CD1(NA,3),CD2(4),IC3(401)
      COMMON/C907A/P(NB,NB),F(NT),IGROUP(NT)
      COMMON/C907B/Y(NB,NB)
      COMMON/C907C/X(NB,NB)
C
      COMMON/IO/IN,IOUT,IODUM(15),NFILE(100,2)
C*
      CHARACTER ITITLE*4,IPROP*24,LABEL*4,ZK*1
C
      DIMENSION CPROP(3),ITITLE(10),VAL(10)
C
 1000 FORMAT(3I4)
 1010 FORMAT(I1,'MO INTEGRALS FOR COMPONENT ',A4,' OF THE ',A24,
     1 '  EVALUATED AT ',A4,' (',F10.5,',',F10.5,',',F10.5,')'//)
 1020 FORMAT(I1,'OEP TRANSFORMATION (IGROUP MO): PROPERTY',I3,
     1 ' FROM UNIT',I3/)
 1030 FORMAT(I1,'OEP TRANSFORMATION (ALL MO): PROPERTY',I3,
     1 ' FROM UNIT',I3/)
 1040 FORMAT (A1)
 1050 FORMAT (2I4,A4,3F12.0/A24,10A4)
 1060 FORMAT (2I4,4F14.0)
 1061 FORMAT (8X,4F14.0)
C*
      IF(IOP(20).EQ.0)GO TO 3
      CALL ATMUL
      RETURN
C
    3 IPAGE=1
      GO TO 2
C
    1 IPAGE=0
    2 READ(IN,1000)IUNIT,NPROP,ICODE
      IF(IUNIT.LE.0)RETURN
      IF(IOP(61).EQ.0)GO TO 30
      IF(ICODE.NE.0)GO TO 30
      WRITE(IOUT,1020)IPAGE,NPROP,IUNIT
      IF(IOP(10).EQ.0)GO TO 1
C     GET MO IN SAME ORDER AS FOR INTEGRAL TRANSFORMATION.
      CALL TREAD(12,F,NFILE(12,1),1,NFILE(12,1),1,0)
      CALL TREAD(25,X,NB,NB,NBASIS,NBASIS,0)
      NMO=0
      DO 20 IG=1,99
      DO 20 I=1,NBASIS
      IF(IGROUP(I).NE.IG)GO TO 20
      NMO=NMO+1
      DO 10 J=1,NBASIS
   10 Y(J,NMO)=X(J,I)
   20 CONTINUE
      GO TO 40
C
C     TRANSFORM ALL MO.
C
   30 WRITE(IOUT,1030)IPAGE,NPROP,IUNIT
      IF(IOP(10).EQ.0)GO TO 1
      CALL TREAD(25,Y,NB,NB,NBASIS,NBASIS,0)
      NMO=NBASIS
C
C     READ AO INTEGRALS FROM IUNIT.
C
   40 REWIND IUNIT
      IC=0
      ISKIP=0
C
C     LOOP OVER COMPONENTS (COMPONENT NUMBER IS 'IC').
C
   50 IC=IC+1
C
C     SKIP TO PROPERTY NPROP ON IUNIT
C
      IF(NPROP.EQ.1)GO TO 100
      J = NPROP - 1
      DO 70 I=1,J
   60 READ (IUNIT,1040) ZK
      IF (ZK .NE. '*') GO TO 60
   70 CONTINUE
C
  100 READ (IUNIT,1050) MPROP,NTPROP,LABEL,CPROP,IPROP,ITITLE
C
  110 J = NTPROP
      IF (J .GT. 4) J = 4
      READ (IUNIT,1060) K,L,(VAL(I),I=1,J)
      IF (NTPROP .LE. 4) GO TO 115
      J = NTPROP
      IF (J .GT. 8) J = 8
      READ (IUNIT,1061) (VAL(I),I=5,J)
      IF (NTPROP .GT. 8) READ (IUNIT,1061) (VAL(I),I=9,NTPROP)
C
  115 P(K,L)=VAL(IC)
      P(L,K)=VAL(IC)
      IF(K.EQ.NBASIS.AND.L.EQ.NBASIS)GO TO 120
      GO TO 110
C
C     IF ROTATORY STRENGTH THEN ANTISYMMETRIZE.
C
  120 REWIND IUNIT
      IF(MPROP.NE.14)GO TO 140
      DO 130 K=1,NBASIS
      DO 130 L=1,K
      IF(K.EQ.L)GO TO 130
      P(K,L)=-P(L,K)
  130 CONTINUE
C
C     TRANSFORM AO TO MO BASIS
C
  140 DO 160 I=1,NBASIS
      DO 160 J=1,NMO
      SUM=ZERO
      DO 150 K=1,NBASIS
  150 SUM=SUM+P(I,K)*Y(K,J)
  160 X(I,J)=SUM
      DO 180 I=1,NMO
      DO 180 J=1,NMO
      SUM=ZERO
      DO 170 K=1,NBASIS
  170 SUM=SUM+Y(K,I)*X(K,J)
  180 P(I,J)=SUM
      WRITE(IOUT,1010)ISKIP,ITITLE(IC),IPROP,LABEL,CPROP
      CALL MATOUT(P,NB,NB,NMO,NMO)
      ISKIP=1
      IF (MPROP .EQ. 4) NTPROP = NTPROP - 1
      IF(IC.LT.NTPROP)GO TO 50
      GO TO 2
      END
      SUBROUTINE ATMUL
C*
C=O.E.P. TRANSFORMATION
C1MULTIPOLE
C     THIS PROGRAM DECOMPOSES THE MOLECULAR MULTIPOLES INTO ATOMIC
C     MONOPOLES, DIPOLES, QUADRUPOLES AND OCTUPOLES.
C
C     AUTHOR: W. A. SOKOLSKI
C             INSTITUTE OF ORGANIC AND PHYSICAL CHEMISTRY
C             WYSPIANSKIEGO 27
C             50-370 WROCLAW, POLAND
C*
C     INPUT REQUIREMENTS:
C
C     IUNIT (I4)
C*
C     IUNIT IS THE UNIT WHERE THE AO INTEGRALS ARE TO BE READ.
C
C     NOTE:
C     BECAUSE OF RECURSIVE CHARACTER OF UTILIZED FORMULA THE FOLLOWING
C     ORDER OF CALCULATIONS HAS TO BE RETAINED IN GL0802 AND IN GL0907
C     NPROP=1  (DIPOLE MOMENT, MPROP=4)
C     NPROP=2  (QUADRUPOLE MOMENT, MPROP=5 OR 7)
C     NPROP=3  (OCTUPOLE MOMENT, MPROP=6 OR 8)
C     THIS CAN BE DONE BY SPECIFYING MPROP=-3 IN GL0802.
C==
C*
C/
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C#
C     PARAMETER (NA=#NA, NB=#NB)
C     PARAMETER (NT=#NT)
C##
      PARAMETER (NA= 36, NB=200)
      PARAMETER (NT= 70)
C###
      PARAMETER (ZERO=0.0D0, TWO=2.0D0, THREE=3.0D0)
C
      COMMON /A/ IOP(99),NATOMS,ICHARG,MULTIP,IAN(NA),NAE,NBE,NE,NBASIS,
     1 C(NA,3),ANTOAU,DCONST,CDUM(2),IC2(401)
      COMMON/C907A/P(NB,NB),FILLA(NT),IFILLA(NT)
      COMMON/C907B/D(NB,NB)
      COMMON/C907C/AT(NA,10),AM(NA),AD(NA,3),AQ(NA,6)
      COMMON /BSINFO/ NVO(NA), IAOFF(NA)
C
      COMMON/IO/IN,IOUT,IODUM(215)
C
      CHARACTER LABEL*4,IPROP*24,ZK*1
C
      DIMENSION CPROP(3),VAL(10)
C     FILL UP COMMON /C907C/.
      DIMENSION FILLC(NB,NB)
C
      EQUIVALENCE (FILLC(1,1),AT(1,1))
C
 1000 FORMAT(I4)
 1010 FORMAT(1X,'AO INTEGRALS OF THE ',A24,'  EVALUATED AT ',A4,' (',
     1 F10.5,',',F10.5,',',F10.5,')'//)
 1020 FORMAT(I1,'OEP ATOMIC DECOMPOSITION      : PROPERTIES ARE READ ',
     1 'FROM UNIT',I4/)
 1030 FORMAT(I1,'OEP ATOMIC DECOMPOSITION   : PROPERTY',I3)
 1040 FORMAT(1X,I4,1X,10F10.6)
 1050 FORMAT(/'0ATOMIC DIPOLES (IN ATOMIC UNITS)'/'0 ATOM',6X,'X',9X,
     1 'Y',9X,'Z'/)
 1060 FORMAT(/'0ATOMIC QUADRUPOLES (IN ATOMIC UNITS)'/'0 ATOM',6X,'XX',
     1 8X,'YY',8X,'ZZ',8X,'XY',8X,'XZ',8X,'YZ'/)
 1070 FORMAT(/'0ATOMIC OCTUPOLES (IN ATOMIC UNITS)'/'0 ATOM',6X,'XXX',
     1 7X,'YYY',7X,'ZZZ',7X,'XXY',7X,'XXZ',7X,'XYY',7X,'YYZ',7X,'XZZ',
     2 7X,'YZZ',7X,'XYZ'/)
 1080 FORMAT('0TOTAL',10F10.6)
 1090 FORMAT (A1)
 1100 FORMAT (4X,I4,A4,3F12.0/A24)
 1110 FORMAT (2I4,4F14.0)
 1111 FORMAT (8X,4F14.0)
C
      IPAGE=1
      READ(IN,1000)IUNIT
      WRITE(IOUT,1020)IPAGE,IUNIT
      IF(IUNIT.LE.0)RETURN
      IF(IOP(10).LE.0)RETURN
C     READ DENSITY MATRIX
      CALL TREAD(19,D,NB,NB,NBASIS,NBASIS,1)
      IF(IOP(9).EQ.0)GO TO 4
      CALL TREAD(21,P,NB,NB,NBASIS,NBASIS,1)
      DO 2 J=1,NBASIS
      DO 2 I=1,J
      D(I,J)=D(I,J)+P(I,J)
    2 D(J,I)=D(I,J)
C*
    4 IPAGE=0
C     READ OVERLAP MATRIX
      CALL TREAD(8,P,NB,NB,NBASIS,NBASIS,1)
      I2=0
      DO 6 I=1,NATOMS
      I1=I2+1
      I2=I2+NVO(I)
      AM(I)=IAN(I)
      IF(IAN(I).LE.0)AM(I)=ZERO
      DO 5 II=I1,I2
      DO 5 J=1,NBASIS
    5 AM(I)=AM(I)-P(II,J)*D(II,J)
    6 CONTINUE
C
C     ATOMIC MONOPOLES IN AM
C
      REWIND IUNIT
C
      DO 300 NPROP=1,3
      WRITE(IOUT,1030)IPAGE,NPROP
      IPAGE=1
      IC=0
C
C     LOOP OVER COMPONENTS (COMPONENT NUMBER IS 'IC').
C
   50 IC=IC+1
C
C     SKIP TO PROPERTY NPROP ON IUNIT.
C
      IF(NPROP.EQ.1)GO TO 100
      J = NPROP - 1
      DO 70 I=1,J
   60 READ (IUNIT,1090) ZK
      IF (ZK .NE. '*') GO TO 60
   70 CONTINUE
C
  100 READ (IUNIT,1100) NTPROP,LABEL,CPROP,IPROP
C
  110 J = NTPROP
      IF (J .GT. 4) J = 4
      READ (IUNIT,1110) K,L,(VAL(I),I=1,J)
      IF (NTPROP .LE. 4) GO TO 115
      J = NTPROP
      IF (J .GT. 8) J = 8
      READ (IUNIT,1111) (VAL(I),I=5,J)
      IF (NTPROP .GT. 8) READ (IUNIT,1111) (VAL(I),I=9,NTPROP)
C
  115 P(K,L)=VAL(IC)
      P(L,K)=VAL(IC)
      IF(K.EQ.NBASIS.AND.L.EQ.NBASIS)GO TO 120
      GO TO 110
C
C     EVALUATE ATOMIC MULTIPOLES - OMIT TOTAL 1'ST AND 2'ND MOMENTS.
C
  120 REWIND IUNIT
      IF(NPROP.LE.2)NTPROP=NTPROP-1
      I2=0
      DO 200 II=1,NATOMS
      I1=I2+1
      I2=I2+NVO(II)
      IF(IAN(II).LE.0)GO TO 200
      AN=IAN(II)
      CIIX=(C(II,1)-CPROP(1))
      CIIY=(C(II,2)-CPROP(2))
      CIIZ=(C(II,3)-CPROP(3))
      CIIX2=CIIX*CIIX
      CIIY2=CIIY*CIIY
      CIIZ2=CIIZ*CIIZ
      GO TO (130,140,150),NPROP
C     UV IS THE NUCLEAR COMPONENT
  130 UV=C(II,IC)-CPROP(IC)
      GO TO 160
C
  140 IF(IC.EQ.1)UV=CIIX2
      IF(IC.EQ.2)UV=CIIY2
      IF(IC.EQ.3)UV=CIIZ2
      IF(IC.EQ.4)UV=CIIX*CIIY
      IF(IC.EQ.5)UV=CIIX*CIIZ
      IF(IC.EQ.6)UV=CIIY*CIIZ
      GO TO 160
C
  150 IF(IC.EQ.1)UV=CIIX2*CIIX
      IF(IC.EQ.2)UV=CIIY2*CIIY
      IF(IC.EQ.3)UV=CIIZ2*CIIZ
      IF(IC.EQ.4)UV=CIIX2*CIIY
      IF(IC.EQ.5)UV=CIIX2*CIIZ
      IF(IC.EQ.6)UV=CIIX*CIIY2
      IF(IC.EQ.7)UV=CIIY2*CIIZ
      IF(IC.EQ.8)UV=CIIX*CIIZ2
      IF(IC.EQ.9)UV=CIIY*CIIZ2
      IF(IC.EQ.10)UV=CIIX*CIIY*CIIZ
C
  160 UV=UV*AN
      GO TO (165,170,175),NPROP
  165 UV=UV-AM(II)*(C(II,IC)-CPROP(IC))
      GO TO 180
C
  170 IF(IC.EQ.1)UV=UV-AM(II)*CIIX2-TWO*AD(II,1)*CIIX
      IF(IC.EQ.2)UV=UV-AM(II)*CIIY2-TWO*AD(II,2)*CIIY
      IF(IC.EQ.3)UV=UV-AM(II)*CIIZ2-TWO*AD(II,3)*CIIZ
      IF(IC.EQ.4)UV=UV-AM(II)*CIIX*CIIY-AD(II,1)*CIIY-AD(II,2)*CIIX
      IF(IC.EQ.5)UV=UV-AM(II)*CIIX*CIIZ-AD(II,1)*CIIZ-AD(II,3)*CIIX
      IF(IC.EQ.6)UV=UV-AM(II)*CIIY*CIIZ-AD(II,2)*CIIZ-AD(II,3)*CIIY
      GO TO 180
C
  175 IF(IC.EQ.1)UV=UV-AM(II)*CIIX2*CIIX-THREE*AD(II,1)*CIIX2
     *                -THREE*AQ(II,1)*CIIX
      IF(IC.EQ.2)UV=UV-AM(II)*CIIY2*CIIY-THREE*AD(II,2)*CIIY2
     *                -THREE*AQ(II,2)*CIIY
      IF(IC.EQ.3)UV=UV-AM(II)*CIIZ2*CIIZ-THREE*AD(II,3)*CIIZ2
     *                -THREE*AQ(II,3)*CIIZ
      IF(IC.EQ.4)UV=UV-AM(II)*CIIX2*CIIY-TWO*AD(II,1)*CIIX*CIIY
     *                -AD(II,2)*CIIX2-TWO*AQ(II,4)*CIIX-AQ(II,1)*CIIY
      IF(IC.EQ.5)UV=UV-AM(II)*CIIX2*CIIZ-TWO*AD(II,1)*CIIX*CIIZ
     *                -AD(II,3)*CIIX2-TWO*AQ(II,5)*CIIX-AQ(II,1)*CIIZ
      IF(IC.EQ.6)UV=UV-AM(II)*CIIY2*CIIX-TWO*AD(II,2)*CIIX*CIIY
     *                -AD(II,1)*CIIY2-TWO*AQ(II,4)*CIIY-AQ(II,2)*CIIX
      IF(IC.EQ.7)UV=UV-AM(II)*CIIY2*CIIZ-TWO*AD(II,2)*CIIY*CIIZ
     *                -AD(II,3)*CIIY2-TWO*AQ(II,6)*CIIY-AQ(II,2)*CIIZ
      IF(IC.EQ.8)UV=UV-AM(II)*CIIZ2*CIIX-TWO*AD(II,3)*CIIX*CIIZ
     *                -AD(II,1)*CIIZ2-TWO*AQ(II,5)*CIIZ-AQ(II,3)*CIIX
      IF(IC.EQ.9)UV=UV-AM(II)*CIIZ2*CIIY-TWO*AD(II,3)*CIIY*CIIZ
     *                -AD(II,2)*CIIZ2-TWO*AQ(II,6)*CIIZ-AQ(II,3)*CIIY
      IF(IC.EQ.10)UV=UV-AM(II)*CIIX*CIIY*CIIZ-AD(II,1)*CIIY*CIIZ
     *              -AD(II,2)*CIIX*CIIZ-AD(II,3)*CIIX*CIIY-AQ(II,4)*CIIZ
     *                -AQ(II,5)*CIIY-AQ(II,6)*CIIX
  180 CONTINUE
C
C     EVALUATE IC-COMPONENT OF THE ATOMIC MOMENT FOR THE II-TH ATOM
C
      DO 190 I=I1,I2
      DO 190 J=1,NBASIS
      UV=UV+D(I,J)*P(I,J)
  190 CONTINUE
      AT(II,IC)=UV
  200 CONTINUE
      IF(IC.LT.NTPROP)GO TO 50
C
C     PRINT ATOMIC MULTIPOLES
C
      WRITE(IOUT,1010)IPROP,LABEL,CPROP
      IF(NPROP.EQ.1)WRITE(IOUT,1050)
      IF(NPROP.EQ.2)WRITE(IOUT,1060)
      IF(NPROP.EQ.3)WRITE(IOUT,1070)
      DO 250 I=1,NATOMS
      DO 240 J=1,NTPROP
      IF(NPROP.EQ.1)AD(I,J)=AT(I,J)
      IF(NPROP.EQ.2)AQ(I,J)=AT(I,J)
  240 CONTINUE
  250 WRITE(IOUT,1040)I,(AT(I,M),M=1,NTPROP)
      DO 270 J=1,NTPROP
      SUM=ZERO
      DO 260 I=1,NATOMS
  260 SUM=SUM+AT(I,J)
  270 VAL(J)=SUM
      WRITE(IOUT,1080)(VAL(M),M=1,NTPROP)
  300 CONTINUE
C
      REWIND IUNIT
      RETURN
      END
