C     GL0803       05 JUN 88                                         MRP
C?IBM/GLD/GBR/VAX/UNX
      SUBROUTINE DIPSQ
C??
C?CDC
C     PROGRAM DIPSQ
C??
C=ONE ELECTRON PROPERTIES
C1INTRODUCTION
C     LINK 0803
C*
C     THIS PROGRAM ESTIMATES COMPONENTS OF DIPOLE POLARIZABILITY
C     BASED ON :
C
C           1) THE CLOSURE APPROXIMATION OF THE DIPOLE POLARIZABILITY
C              EQUATION FROM SECOND ORDER PERTURBATION THEORY,
C
C           2) KOOPMAN'S THEOREM, USING THE ELECTRONIC ORBITAL ENERGIES
C              AS ESTIMATES OF IONIZATION ENERGIES.
C
C     AUTHOR: MAURICE SYLVAIN,
C     DEPARTMENT OF CHEMISTRY, UNIVERSITY OF TORONTO ,TORONTO, CANADA.
C     VERSION: JANUARY 1987.
C
C1OPTIONS
C     ******************************************************************
C     IOP() ... OPTIONS.
C     ******************************************************************
C     IOP(13) TO IOP(19) ... USED BY LINK 0801.
C
C     IOP(20) TO IOP(24) ... USED BY LINK 0802.
C
C     IOP(25) = 0 NORMAL O.E.P. CALCULATIONS.
C               1 ESTIMATE POLARIZABILITY.
C               2 ESTIMATE POLARIZABILITY AND DO O.E.P. CALCULATIONS.
C
C     IOP(26) TO IOP(60) ... NOT USED.
C     ******************************************************************
C==
C
C/
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C#
C     PARAMETER (NA=#NA, NB=#NB)
C##
      PARAMETER (NA= 36, NB=200)
C###
      PARAMETER (ZERO=0.0D0, ONE=1.0D0, TWO=2.0D0, THREE=3.0D0,
     1 SIX=6.0D0)
C
      COMMON /A/ IOP(99)
      COMMON /A/ NATOMS,ICHARG,MULTIP,IAN(NA),NAE,NBE,NE,NBASIS,C(NA,3)
      COMMON /A/ ANTOAU,DUMC(3),IDUMC(401)
      COMMON /C803/ EIG(NB), D(NB,NB), V1(NB,NB), V2(NB,NB),
     1 V3(NB,NB)
C
      COMMON/IO/IN,IOUT,IPUNCH,IMAT,ITWOEL,IODUM(12),NFILE(100,2)
C
      CHARACTER XYZ*1
C
      DIMENSION V(3,3), Q(6), XYZ(3)
C
      DATA XYZ /'X', 'Y', 'Z'/
C
 1000 FORMAT('1ESTIMATE OF DIPOLE POLARIZABILITY:'/
     1 ' ',34('*'))
 1010 FORMAT(1X,A1,F12.6,2(5X,F12.6))
 1020 FORMAT(///' ESTIMATED AVERAGE POLARIZABILITY ',F12.6///
     1 ' ESTIMATED ANISOTROPY ',F12.6)
 1030 FORMAT (/'0RESULTS IN ATOMIC UNITS (BOHR**3)'/)
 1040 FORMAT (/'0RESULTS IN ANGSTROMS**3'/)
 1050 FORMAT ('0',8X,'X',16X,'Y',16X,'Z')
 1060 FORMAT (//' AVERAGE IONIZATION ( BASED ON -SUM(1/ORBITAL E) ) :',
     1 F12.6)
 1070 FORMAT (//' ELECTRONIC SECOND MOMENT TENSOR:')
 1080 FORMAT (//' EXCHANGE TENSOR:')
 1090 FORMAT (//' CALCULATED POLARIZABILITY TENSOR:')
 1100 FORMAT (//12X,'AVERAGE:',F12.6)
 1110 FORMAT (//' VALUE OF (DELTA)*(AVERAGE POLARIZABILITY) :',F12.6)
C
      IF (IOP(10) .EQ. 0) RETURN
      WRITE (IOUT,1000)
C
C     READ ELECTRONIC SECOND MOMENTS EVALUATED AT THE ORIGIN,
C     CALCULATED FROM ONE ELECTRON PROPERTIES PACKAGE,
C     STORED IN UNIT=IMAT, FILE=4.
C
      CALL TREAD (4, Q, 6, 1, 6, 1, 0)
C
C     READ DENSITY MATRIX INTO D.
C
      CALL TREAD (19, D, NB, NB, NBASIS, NBASIS, 1)
C
C     READ X DIPOLE MOMENT INTEGRALS INTO V1, AND FORM D*V1.
C
      CALL TREAD (34, V1, NB, NB, NBASIS, NBASIS, 1)
      CALL MAPRO1 (V1, NBASIS, NBASIS, NBASIS, D, EIG)
C
C     READ Y DIPOLE MOMENT INTERGALS INTO V2, AND FORM D*V2.
C
      CALL TREAD (35, V2, NB, NB, NBASIS, NBASIS, 1)
      CALL MAPRO1 (V2, NBASIS, NBASIS, NBASIS, D, EIG)
C
C     READ Z DIPOLE MOMENT INTEGRALS INTO V3, AND FORM D*V3.
C
      CALL TREAD (36, V3, NB, NB, NBASIS, NBASIS, 1)
      CALL MAPRO1 (V3, NBASIS, NBASIS, NBASIS, D, EIG)
C
C     READ ORBITAL ENERGIES INTO EIG.
C
      CALL TREAD (5, EIG, NB, 1, NBASIS, 1, 0)
C
C     FORM ORBITAL ENERGY AVERAGE.
C
      NOCC = NE / 2
      G = ZERO
      DO 10 I=1,NOCC
   10 G = G + ONE / EIG(I)
      G =  G / DFLOAT(NOCC)
C
C     CALCULATE EXCHANGE TERM
C
      CALL TRACE4 (V1, V1, NB, NBASIS, VAL)
      V(1,1) = VAL / TWO
      CALL TRACE4 (V1, V2, NB, NBASIS, VAL)
      V(1,2) = VAL / TWO
      V(2,1) = V(1,2)
      CALL TRACE4 (V1, V3, NB, NBASIS, VAL)
      V(1,3) = VAL / TWO
      V(3,1) = V(1,3)
      CALL TRACE4 (V2, V2, NB, NBASIS, VAL)
      V(2,2) = VAL / TWO
      CALL TRACE4 (V2, V3, NB, NBASIS, VAL)
      V(2,3) = VAL / TWO
      V(3,2) = V(2,3)
      CALL TRACE4 (V3, V3, NB, NBASIS, VAL)
      V(3,3) = VAL / TWO
C
C     WRITE OUT 1/DELTA, THE ELECTRONIC SECOND MOMENT, AND THE
C     EXCHANGE TERM.
C
      WRITE(IOUT,1060) -G
      WRITE(IOUT,1070)
      WRITE(IOUT,1050)
      WRITE(IOUT,1010) XYZ(1), Q(1), Q(4), Q(5)
      WRITE(IOUT,1010) XYZ(2), Q(4), Q(2), Q(6)
      WRITE(IOUT,1010) XYZ(3), Q(5), Q(6), Q(3)
      AV = ( Q(1) + Q(2) + Q(3) ) / THREE
      WRITE(IOUT,1100) AV
      AV1 = ZERO
      WRITE(IOUT,1080)
      WRITE(IOUT,1050)
      DO 15 I=1,3
      AV1 = AV1 + V(I,I)
  15  WRITE(IOUT,1010) XYZ(I), V(I,1), V(I,2), V(I,3)
      AV1 = AV1 / THREE
      WRITE(IOUT,1100) AV1
C
      AV = -TWO*(AV + AV1)
      WRITE(IOUT,1110) AV
C
C     CALCULATE POLARIZABILITY COMPONENTS, STORED AS V(I,J).
C     I = 1 FOR X, I = 2 FOR Y, I = 3 FOR Z.
C
      V(1,1) = G * TWO * ( Q(1) + V(1,1) )
      V(1,2) = G * TWO * ( Q(4) + V(1,2) )
      V(2,1) = V(1,2)
      V(1,3) = G * TWO * ( Q(5) + V(1,3) )
      V(3,1) = V(1,3)
      V(2,2) = G * TWO * ( Q(2) + V(2,2) )
      V(2,3) = G * TWO * ( Q(6) + V(2,3) )
      V(3,2) = V(2,3)
      V(3,3) = G * TWO * ( Q(3) + V(3,3) )
C
C     WRITE OUT RESULT IN BOHR**3.
C
      AX = V(1,1)
      AY = V(2,2)
      AZ = V(3,3)
      AV = (AX + AY + AZ) / THREE
      AXX = AX - AV
      AYY = AY - AV
      AZZ = AZ - AV
      ANNI = AXX*AXX + AYY*AYY + AZZ*AZZ
      IF (AV .NE. ZERO) ANNI = DSQRT(ANNI/(SIX*AV*AV))
      WRITE(IOUT,1090)
      WRITE (IOUT,1030)
      WRITE (IOUT,1050)
      DO 20 I=1,3
   20 WRITE (IOUT,1010) XYZ(I), V(I,1), V(I,2), V(I,3)
      WRITE (IOUT,1020) AV, ANNI
C
C     WRITE OUT RESULT IN ANGSTROMS**3.
C
      WRITE (IOUT,1040)
      WRITE (IOUT,1050)
      T = ONE / ANTOAU**3
      AV = AV * T
      DO 30 I=1,3
      V(I,1) = V(I,1) * T
      V(I,2) = V(I,2) * T
      V(I,3) = V(I,3) * T
   30 WRITE (IOUT,1010) XYZ(I), V(I,1), V(I,2), V(I,3)
      WRITE (IOUT,1020) AV, ANNI
      RETURN
      END
      SUBROUTINE TRACE4 (A, B, NB, NBF, VAL)
C
C     CALCULATE VAL = TRACE (A*B).
C     A AND B DECLARED AS NB*NB, USED PORTION IS NBF*NBF.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      DIMENSION A(NB,NB), B(NB,NB)
C
      DATA ZERO/0.0D0/
C
      VAL = ZERO
      DO 10 IP=1,NBF
      DO 10 IR=1,NBF
   10 VAL = VAL + A(IP,IR)*B(IR,IP)
      RETURN
      END
