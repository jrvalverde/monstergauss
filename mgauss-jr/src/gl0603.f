C     GL0603       23 NOV 87                                         MRP
C?GLD/GBR/IBM/VAX/UNX
      SUBROUTINE ZMOROK
C??
C?CDC
C     PROGRAM ZMOROK
C??
C=ENERGY DECOMPOSITION / BSSE
C CHAPTER 10: ENERGY DECOMPOSITION / BSSE.
C ***********
C
C1INTRODUCTION
C     LINK 0603
C
C     --------------
C     U OF T VERSION
C     JULY 1987
C     --------------
C
C     ENERGY DECOMPOSITION / BSSE CONTROL ROUTINE.
C
C     ORIGINAL: R. CAMMI, U. OF PARMA, NOVEMBER 1985.
C     RE-WRITTEN: M. PETERSON, U. OF TORONTO CHEMISTRY DEPARTMENT,
C     JULY 1987.
C
C     REFERENCES FOR ENERGY DECOMPOSITION:
C     1) K. KITAURA AND K. MOROKUMA, I.J.Q.C., 10, 325-340 (1976).
C     2) K. MOROKUMA, ACCTS. CHEM. RES., 10, 294-300 (1977).
C     3) K. MOROKUMA AND K. KITAURA, IN "CHEMICAL APPLICATIONS OF
C     ATOMIC AND MOLECULAR ELECTROSTATIC POTENTIALS", P. POLITZER AND
C     D.G. TRUHLAR (EDS), PLENUM, NEW YORK (1981), PP 215-242.
C
C     REFERENCE FOR BASIS SET SUPERPOSITION ERROR CORRECTION:
C     1) R. CAMMI, R. BONACCORSI AND J. TOMASI, THEOR. CHIM. ACTA,
C     68, 271-283 (1985).
C
C     ******************************************************************
C     REQUIRED INPUT (FOR FIRST ENTRY INTO THIS ROUTINE ONLY)
C     ******************************************************************
C
C     (1)  (IAMAP(I),I=1,NATOMS)            (36I2 FORMAT)
C     WHERE IAMAP IS SET TO 1 OR 2 TO INDICATE WHICH MOLECULE
C     ATOM I BELONGS TO, AND NATOMS IS THE NUMBER OF
C     ATOMS IN THE SUPERMOLECULE (I.E. MOLECULE A (OR 1) +
C     MOLECULE B (OR 2)). DUMMY ATOMS MAY BE ASSIGNED TO EITHER
C     MOLECULE AT WILL.
C
C     (2)  ICHARA, MULTA                    (2I2 FORMAT)
C     WHERE ICHARA AND MULTA ARE THE CHARGE AND MULTIPLICITY
C     FOR MOLECULE A.
C
C     (3)  ICHARB, MULTB                    (2I2 FORMAT)
C     WHERE ICHARB AND MULTB ARE THE CHARGE AND MULTIPLICITY
C     FOR MOLECULE B.
C
C=ENERGY DECOMPOSITION / BSSE
C     ******************************************************************
C     INPUT ORDER FOR ED/BSSE RUNS
C     ******************************************************************
C
C     (1)  *MOL ... WITH 'ED' AND/OR 'BSSE' OPTIONS SPECIFIED.
C     OTHER ROUTE CONTROL CARDS (*PRT, *PUN, *IOP AND/OR *CON)
C     MAY ALSO BE USED.
C
C     (2)  TITLE / CHARGE-MULTIPLICITY / GEOMETRY OF SUPERMOLECULE A+B.
C
C     (3)  SUPERMOLECULE A+B BASIS SET INPUT (IF GENERAL BASIS).
C
C     (4)  ED/BSSE CONTROL DATA (SEE ABOVE).
C
C     (5)  MOLECULE A BASIS SET INPUT (IF GENERAL BASIS).
C
C     (6)  RHF INPUT FOR MOLECULE A (IF RHF), OR ALTERATION OF
C     CONFIGURATION DATA FOR MOLECULE A (IF NECESSARY).
C
C     (7)  MOLECULE B BASIS SET INPUT (IF GENERAL BASIS).
C
C     (8)  RHF INPUT FOR MOLECULE B (IF RHF), OR ALTERATION OF
C     CONFIGURATION DATA FOR MOLECULE B (IF NECESSARY).
C
C     (9)  SUPERMOLECULE A+B BASIS SET INPUT (IF GENERAL BASIS).
C     THIS MEANS THE A+B BASIS SET MUST BE INPUT TWICE, UNFORTUNATELY.
C
C     (10)  RHF INPUT FOR SUPERMOLECULE A+B (IF RHF). NOTE THAT THE
C     ORBITAL ORDER WILL BE PRE-DETERMINED BY THE MO'S FROM THE
C     MOLECULE CALCULATIONS, WITH ALL FROZEN ORBITALS FIRST, THEN
C     ALL OPEN SHELL ORBITALS, THEN ALL VIRTUAL ORBITALS. WITHIN
C     EACH GROUP, THE MO'S FROM MOLECULE A WILL PRECEED THOSE FROM
C     MOLECULE B.
C
C=ENERGY DECOMPOSITION / BSSE
C1ROUTE-GENERATION
C
C     ROUTE STRUCTURE EMPLOYED FOR ENERGY DECOMPOSITION /
C     BASIS SET SUPERPOSITION ERROR CORRECTION:
C
C                                      PHASE
C       LINKS      1   2   3   4   5   6   7   8   9  10  11  12
C     ---------    -   -   -   -   -   -   -   -   -  --  --  --
C        0202      *
C        0301      *   *   *   *
C        0302          *   *   *           *      (*)  *      (*)
C        0303         (*) (*) (*)         (*)     (*) (*)     (*)
C     0306-0308        *   *   *
C        0311                          *
C        0401         (*) (*) (*)
C        0402          *   *
C        0403                  *   *   *   *   *   *   *   *   *
C     0501-0503        *   *   *       *
C     0505-0507                    *       *   *   *   *   *   *
C        0601          *   *   *  (*) (*) (*) (*) (*) (*) (*) (*)
C        0603      *   *   *   *   *   *   *   *   *   *   *   *
C
C     ED ONLY      *   *   *   *   *   *
C     BSSE ONLY    *   *   *   *                   *           *
C     ED + BSSE    *   *   *   *   *   *   *   *   *   *   *   *
C
C     WHERE * INDICATES THAT THE LINK IS REQUIRED, AND (*) INDICATES
C     THAT THE LINK IS OPTIONAL (DEPENDING ON OTHER OPTIONS USED).
C
C     STEP         FUNCTION
C     ----         -----------------------------------------------------
C       1          A+B GEOMETRY, BASIS SET, GENERAL SETUP.
C       2          MOLECULE A SCF (MOLECULE A BASIS).
C       3          MOLECULE B SCF (MOLECULE B BASIS).
C       4          A+B SUPERMOLECULE SCF (A+B BASIS).
C       5          A+B SUPERMOLECULE SCF FOR CT INTERATION.
C       6          A+B SUPERMOLECULE SCF, NO EXCHANGE INTERACTION.
C       7          A+B SUPERMOLECULE SCF, CT(A) BSSE.
C       8          A+B SUPERMOLECULE SCF, EX(A) BSSE.
C       9          A+B SUPERMOLECULE SCF, BSSE FOR MOLECULE A.
C      10          A+B SUPERMOLECULE SCF, CT(B) BSSE.
C      11          A+B SUPERMOLECULE SCF, EX(B) BSSE.
C      12          A+B SUPERMOLECULE SCF, BSSE FOR MOLECULE B.
C
C=ENERGY DECOMPOSITION / BSSE
C1OPTIONS
C     ******************************************************************
C     OPTIONS ... IOP( )
C     ******************************************************************
C     IOP(13) TO IOP(60) ... SEE LINK 0601.
C     ******************************************************************
C==
C
C/
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
      INTEGER SHELLA, SHELLN, SHELLT, SHELLC, AOS
C#
C     PARAMETER (NA=#NA, NB=#NB)
C     PARAMETER (NS=#NS, NP=#NP)
C     PARAMETER (NF=#NF, NC=#NC)
C##
      PARAMETER (NA= 36, NB=200)
      PARAMETER (NS=120, NP=300)
      PARAMETER (NF=120, NC=120)
C###
      PARAMETER (NFCON=20)
      PARAMETER (ZERO=0.0D0, TEN3=1000.0D0)
C
      CHARACTER LABEL*14
C
      COMMON /A/ IOP(99)
      COMMON /A/ NATOMS,ICHARG,MULTIP,IAN(NA),NAE,NBE,NE,NBASIS,C(NA,3)
      COMMON /A/ DUMC(4),IDUMC(401)
C
      COMMON/B/IXYZ(NS),SHELLA(NS),SHELLN(NS),SHELLT(NS),
     1 SHELLC(NS),AOS(NS),NSHELL,MAXTYP,EXX(NP),C1(NP),C2(NP)
      COMMON/GEN/E1,E2,E3,EDUM(3),DCONV,SPIN(2),FX,FY,FZ,FE,EMOL,ESOL,
     1 DPOL(4),VCM
      COMMON/IO/IN,IOUT,IODUM(15),NFILE(100,2)
C
C     ED/BSSE SAVE DATA (IBMAP ... ZEN):
C
C     IBMAP  - MAP BETWEEN BASIS FUNCTIONS AND MOLECULAR FRAGMENTS.
C     IAMAP  - MAP BETWEEN ATOMS AND MOLECULAR FRAGMENTS.
C     IANSAV - A+B ATOMIC NUMBER SAVE AREA.
C
C     FOR EACH OF THE FOLLOWING, THERE ARE 3 ARRAY ELEMENTS:
C     (1) IS FOR MOLECULE A, (2) IS FOR MOLECULE B, AND (3) IS FOR A+B:
C     ICHSAV - MOLECULAR CHARGE SAVE AREA.
C     IMUSAV - MOLECULAR MULTIPLICITY SAVE AREA.
C     NAESAV - NUMBER OF ALPHA SPIN ELECTRONS SAVE AREA.
C     NBESAV - NUMBER OF BETA SPIN ELECTRONS SAVE AREA.
C     NESAV  - NUMBER OF ELECTRONS SAVE AREA.
C     NBASAV - NUMBER OF BASIS FUNCTIONS SAVE AREA.
C
C     FOR EACH OF THE FOLLOWING, THERE ARE 2 ARRAY ELEMENTS:
C     (1) IS FOR CLOSED SHELL, RHF, AND ALPHA SPIN UHF, AND
C     (2) IS FOR BETA SPIN UHF:
C     NOCCAS - NUMBER OF MOLECULE A OCCUPIED MO SAVE AREA.
C     NVACAS - NUMBER OF MOLECULE A VIRTUAL MO SAVE AREA.
C     NOCCBS - NUMBER OF MOLECULE B OCCUPIED MO SAVE AREA.
C     NVACBS - NUMBER OF MOLECULE B VIRTUAL MO SAVE AREA.
C     NBACPC - NUMBER OF BASIS FUNCTIONS SAVE AREA.
C
C     ZEN    - CYCLE 1 AND CONVERGED SCF ENERGY SAVE AREA.
C
      COMMON /C0603/ FUNCON(NFCON),
     1 IBMAP(NB),IAMAP(NA),IANSAV(NA),ICHSAV(3),IMUSAV(3),NAESAV(3),
     2 NBESAV(3),NESAV(3),NBASAV(3),NOCCAS(2),NVACAS(2),NOCCBS(2),
     3 NVACBS(2),NBACPC(2),ZEN(12,2),
     4 RHFEIG(NB,2),RN(NF),RFILL1(119),COEF(NC),RFILL2(18),ICOEF(NC),
     5 IRHF,NCONF,NCORE,NVAL,NOPEN,NFOCK,IMO(4),
     6 C12(NB,NB)
C
      EQUIVALENCE (AIBMAP,IBMAP(1))
C
 9000 FORMAT ('0*** CONSISTENCY CHECK ERROR IN ZMOROK (LINK 0603):'/
     1 5X,'NBASIS (MOLECULE A) =',I5,', NBASIS (MOLECULE B) =',I5/
     2 5X,'BUT NBASIS (A+B) =',I5,' (PHASE 1) AND NBASIS (A+B) =',I5,
     3 ' (PHASE 4)'/5X,'NUMBERS OF BASIS FUNCTIONS MUST BE CONSISTENT.')
 9010 FORMAT (36I2)
 9020 FORMAT ('1MOROKUMA ENERGY DECOMPOSITION / BASIS SET ',
     1 'SUPERPOSITION ERROR CORRECTION'//
     2 '0MAP BETWEEN ATOMS AND MOLECULES:'/
     3 '0ATOM NUMBER:',36I3)
 9030 FORMAT ('0MOLECULE   :',36I3)
 9040 FORMAT ('0*** ILLEGAL MOLECULE NUMBER',I4,' FOR ATOM NUMBER',I4)
 9050 FORMAT (/'0MAP BETWEEN BASIS FUNCTIONS AND MOLECULES:'/
     1 '0A.O. NUMBER:',36I3)
 9060 FORMAT (13X,36I3)
 9070 FORMAT (2I2)
 9080 FORMAT (/'0CHARGE AND MULTIPLICITY FOR MOLECULE A:',I3,I2)
 9090 FORMAT (' CHARGE AND MULTIPLICITY FOR MOLECULE B:',I3,I2)
 9100 FORMAT ('1ENERGY DECOMPOSITION / BASIS SET SUPERPOSITION',
     1 ' ERROR SUMMARY'//
     2 '0MOLECULE A:',F16.6,10X,'MOLECULE B:',F16.6,10X,'A+B:',F16.6,
     3 '   HARTREES'/)
 9110 FORMAT ('0'/'0'/'0INTERACTION ENERGY:    ',F10.3,1X,A)
 9120 FORMAT ('0',58X,'ES',12X,'PL',12X,'EX',12X,'CT',12X,'MIX'/
     1 '0INTERACTION ENERGY COMPONENTS     ',A14,5F14.3)
 9130 FORMAT (//'0DO MOLECULE A.'//)
 9140 FORMAT (//'0DO MOLECULE B.'//)
 9150 FORMAT (//'0DO A+B SUPERMOLECULE.'//)
 9160 FORMAT (//'0DO A+B SUPERMOLECULE (CT INTERACTION ONLY).'//)
 9170 FORMAT (//'0DO A+B SUPERMOLECULE (NO EXCHANGE INTERACTIONS).'//)
 9180 FORMAT (//'0DO A+B SUPERMOLECULE (CT(A) BSSE: OCC A + VAC B).'//)
 9190 FORMAT (//'0DO A+B SUPERMOLECULE (EX(A) BSSE: OCC A + OCC B).'//)
 9200 FORMAT (//'0DO A+B SUPERMOLECULE (BSSE FOR A: FULL AO SPACE).'//)
 9210 FORMAT (//'0DO A+B SUPERMOLECULE (CT(B) BSSE: OCC B + VAC A).'//)
 9220 FORMAT (//'0DO A+B SUPERMOLECULE (EX(B) BSSE: OCC B + OCC A).'//)
 9230 FORMAT (//'0DO A+B SUPERMOLECULE (BSSE FOR B: FULL AO SPACE).'//)
 9240 FORMAT ('0INTERACTION ENERGY CPC:',F10.3,1X,A)
 9250 FORMAT ('0INTERACTION ENERGY COMPONENTS CPC ',A14,28X,3F14.3)
 9260 FORMAT (/'0MONOMER CONTRIBUTIONS TO COUNTERPOISE CORRECTIONS ',A/
     1 '0',21X,'EX',12X,'CT',12X,'T ',12X,'MIX'/
     2 '0MOLECULE A:',4F14.3/'0MOLECULE B:',4F14.3)
 9270 FORMAT (' MOLECULE A:',F16.6,10X,'MOLECULE B:',F16.6,
     1 '   HARTREES WITH COUNTERPOISE CORRECTION (FULL AO SPACE)'/)
C
C
C     EXCEPT ON THE FIRST PASS, RECOVER SAVED DATA.
C
      IF (IOP(4) .GT. 1)
     1 CALL TREAD (44, AIBMAP, NFILE(44,1), 1, NFILE(44,1), 1, 0)
C
C     BRANCH TO CORRECT CODE FOR THIS PASS.
C
      GO TO (100,200,300,400,500,600,700,800,900,1000,1100,1200),
     1 IOP(4)
C
C     STEP 1: PREPARE FOR FUTURE PASSES.
C     **********************************
C
C     READ MAP BETWEEN ATOMS AND MOLECULES.
C
  100 J = MIN0 (NATOMS, 36)
      WRITE (IOUT,9020) (I,I=1,J)
      IF (NATOMS .GT. 36) WRITE (IOUT,9060) (I,I=37,NATOMS)
      READ (IN,9010) (IAMAP(I),I=1,NATOMS)
      WRITE (IOUT,9030) (IAMAP(I),I=1,J)
      IF (NATOMS .GT. 36) WRITE (IOUT,9060) (IAMAP(I),I=37,J)
C
      DO 110 I=1,NATOMS
      IF (IAMAP(I).LE.0 .OR. IAMAP(I).GT.2) THEN
         WRITE (IOUT,9040) IAMAP(I), I
         IOP(1) = -2
         RETURN
      END IF
  110 CONTINUE
C
C     GET CHARGE/MULTIPLICITY OF MOLECULES A AND B.
C
      READ (IN,9070) ICHSAV(1), IMUSAV(1)
      WRITE (IOUT,9080) ICHSAV(1), IMUSAV(1)
      READ (IN,9070) ICHSAV(2), IMUSAV(2)
      WRITE (IOUT,9090) ICHSAV(2), IMUSAV(2)
      ICHSAV(3) = ICHARG
      IMUSAV(3) = MULTIP
C
C     CONVERT MAP BY ATOMS TO A MAP BY ATOMIC ORBITALS.
C
      DO 120 ISHELL=1,NSHELL
      IX = IXYZ(ISHELL)
      ISTART = 1
C     S SHELL.
      IF (SHELLT(ISHELL) .EQ. 0) THEN
         IEND = 1
C     SP OR P SHELL.
      ELSE IF (SHELLT(ISHELL) .EQ. 1) THEN
         IF (SHELLC(ISHELL) .EQ. 1) ISTART = 2
         IEND = 4
C     D SHELL - WATCH OUT FOR 6D OR 5D.
      ELSE IF (SHELLT(ISHELL) .EQ. 2) THEN
         IEND = 6 - IOP(8)
C     F SHELL - WATCH OUT FOR 10F OR 7F.
      ELSE
         IEND = 10 - 3*IOP(8)
      END IF
      DO 120 I=ISTART,IEND
      J = AOS(ISHELL) + I - 1
  120 IBMAP(J) = IAMAP(IX)
C
      J = MIN0 (NBASIS, 36)
      WRITE (IOUT,9050) (I,I=1,J)
      IF (NBASIS .GT. 36) WRITE (IOUT,9060) (I,I=37,NBASIS)
      WRITE (IOUT,9030) (IBMAP(I),I=1,J)
      IF (NBASIS .GT. 36) WRITE (IOUT,9060) (IBMAP(I),I=37,NBASIS)
C
C     SETUP AND SAVE REMAINING DATA.
C
      DO 130 I=1,NATOMS
  130 IANSAV(I) = IAN(I)
      NAESAV(1) = 0
      NAESAV(2) = 0
      NAESAV(3) = NAE
      NBESAV(1) = 0
      NBESAV(2) = 0
      NBESAV(3) = NBE
      NESAV(1) = 0
      NESAV(2) = 0
      NESAV(3) = NE
      NBASAV(1) = 0
      NBASAV(2) = 0
      NBASAV(3) = NBASIS
      NOCCAS(1) = 0
      NOCCAS(2) = 0
      NVACAS(1) = 0
      NVACAS(2) = 0
      NOCCBS(1) = 0
      NOCCBS(2) = 0
      NVACBS(1) = 0
      NVACBS(2) = 0
      NBACPC(1) = 0
      NBACPC(2) = 0
      DO 140 I=1,12
      ZEN(I,1) = ZERO
  140 ZEN(I,2) = ZERO
C
C     SET UP FOR MOLECULE A (CHANGE MOLECULE B ATOMS TO DUMMY ATOMS).
C
      ICHARG = ICHSAV(1)
      MULTIP = IMUSAV(1)
      DO 150 I=1,NATOMS
  150 IF (IAMAP(I) .EQ. 2) IAN(I) = 0
      WRITE (IOUT,9130)
      GO TO 2000
C
C     STEP 2: COMPLETED MOLECULE A SCF.
C     *********************************
C
C     SAVE TOTAL ENERGY OF MOLECULE A.
C     SAVE ALPHA (AND BETA IF NECESSARY) MO COEFFICIENTS.
C     SAVE RHF DATA (IF NECESSARY).
C
  200 NAESAV(1) = NAE
      NBESAV(1) = NBE
      NESAV(1) = NE
      NBASAV(1) = NBASIS
      ZEN(2,2) = E3
      IF (IOP(10) .NE. 0) THEN
         CALL TREAD (25, C12, NB, NB, NBASIS, NBASIS, 0)
         CALL TWRITE (40, C12, NB, NB, NBASIS, NBASIS, 0)
         IF (IOP(9) .EQ. 1) THEN
            CALL TREAD (27, C12, NB, NB, NBASIS, NBASIS, 0)
            CALL TWRITE (41, C12, NB, NB, NBASIS, NBASIS, 0)
         END IF
      END IF
      IF (IOP(9) .EQ. 2) THEN
         CALL TREAD (5, RHFEIG, NFILE(5,1), 1, NFILE(5,1), 1, 0)
         CALL TWRITE (46, RHFEIG, NFILE(46,1), 1, NFILE(46,1), 1, 0)
      END IF
C
C     SET UP FOR MOLECULE B (CHANGE MOLECULE A ATOMS TO DUMMY ATOMS).
C
      ICHARG = ICHSAV(2)
      MULTIP = IMUSAV(2)
      DO 210 I=1,NATOMS
      IAN(I) = IANSAV(I)
  210 IF (IAMAP(I) .EQ. 1) IAN(I) = 0
      WRITE (IOUT,9140)
      GO TO 2000
C
C     STEP 3: COMPLETED MOLECULE B SCF.
C     *********************************
C
C     SAVE TOTAL ENERGY OF MOLECULE B.
C     SAVE ALPHA (AND BETA IF NECESSARY) MO COEFFICIENTS.
C     SAVE RHF DATA (IF NECESSARY).
C
  300 NAESAV(2) = NAE
      NBESAV(2) = NBE
      NESAV(2) = NE
      NBASAV(2) = NBASIS
      ZEN(3,2) = E3
      IF (IOP(10) .NE. 0) THEN
         CALL TREAD (25, C12, NB, NB, NBASIS, NBASIS, 0)
         CALL TWRITE (42, C12, NB, NB, NBASIS, NBASIS, 0)
         IF (IOP(9) .EQ. 1) THEN
            CALL TREAD (27, C12, NB, NB, NBASIS, NBASIS, 0)
            CALL TWRITE (43, C12, NB, NB, NBASIS, NBASIS, 0)
         END IF
      END IF
      IF (IOP(9) .EQ. 2) THEN
         CALL TREAD (5, RHFEIG, NFILE(5,1), 1, NFILE(5,1), 1, 0)
         CALL TWRITE (47, RHFEIG, NFILE(47,1), 1, NFILE(47,1), 1, 0)
      END IF
C
C     RESTORE ATOM NUMBERS.
C
      ICHARG = ICHSAV(3)
      MULTIP = IMUSAV(3)
      DO 310 I=1,NATOMS
  310 IAN(I) = IANSAV(I)
      WRITE (IOUT,9150)
      GO TO 2000
C
C     STEP 4: COMPLETED SUPERMOLECULE A+B SCF.
C     ****************************************
C
C     SAVE TOTAL ENERGY OF A+B, BOTH INITIAL AND FINAL.
C
  400 IF (NBASAV(1)+NBASAV(2).NE.NBASIS .OR. NBASAV(3).NE.NBASIS) THEN
         WRITE (IOUT,9000) NBASAV, NBASIS
         IOP(1) = -2
         RETURN
      END IF
      IF (IOP(10) .EQ. 0) THEN
         IOP(1) = 1
         RETURN
      END IF
      ZEN(4,1) = EDUM(1)
      ZEN(4,2) = E3
C
C     IF THIS IS A BSSE ONLY RUN, SKIP SOME OF THE ED PHASES.
C
      IF (IOP(29) .EQ. 2) THEN
         IOP(4) = 8
         GO TO 810
      END IF
C
      WRITE (IOUT,9160)
      GO TO 2000
C
C     STEP 5: COMPLETED SUPERMOLECULE A+B SCF, CT INTERATION ONLY.
C     ************************************************************
C
C     SAVE TOTAL ENERGY OF A+B, BOTH INITIAL AND FINAL.
C
  500 ZEN(5,1) = EDUM(1)
      ZEN(5,2) = E3
      WRITE (IOUT,9170)
      GO TO 2000
C
C     STEP 6: COMPLETED SUPERMOLECULE A+B SCF, NO MOLECULE OVERLAP.
C     *************************************************************
C
C     SAVE TOTAL ENERGY OF A+B, BOTH INITIAL AND FINAL.
C
  600 ZEN(6,1) = EDUM(1)
      ZEN(6,2) = E3
C
C     STOP ED/BSSE IF ENERGY DECOMPOSITION ONLY.
C
      IF (IOP(29) .EQ. 1) GO TO 1210
C
C     SET UP FOR MOLECULE A (CHANGE MOLECULE B ATOMS TO FLOATING ATOMS).
C
      ICHARG = ICHSAV(1)
      MULTIP = IMUSAV(1)
      NAE = NAESAV(1)
      NBE = NBESAV(1)
      NE = NESAV(1)
      DO 610 I=1,NATOMS
  610 IF (IAMAP(I).EQ.2 .AND. IAN(I).GT.0) IAN(I) = -1
      WRITE (IOUT,9180)
      GO TO 2000
C
C     STEP 7: COMPLETED BSSE FOR CT(A): OCC A + VAC B.
C     ************************************************
C
C     SAVE TOTAL ENERGY OF A, BOTH INITIAL AND FINAL.
C
  700 ZEN(7,1) = EDUM(1)
      ZEN(7,2) = E3
      WRITE (IOUT,9190)
      GO TO 2000
C
C     STEP 8: COMPLETED BSSE FOR EX(A): OCC A + OCC B.
C     ************************************************
C
C     SAVE TOTAL ENERGY OF A, BOTH INITIAL AND FINAL.
C
  800 ZEN(8,1) = EDUM(1)
      ZEN(8,2) = E3
C
C     SET UP FOR MOLECULE A (CHANGE MOLECULE B ATOMS TO FLOATING ATOMS).
C
  810 ICHARG = ICHSAV(1)
      MULTIP = IMUSAV(1)
      NAE = NAESAV(1)
      NBE = NBESAV(1)
      NE = NESAV(1)
      DO 820 I=1,NATOMS
  820 IF (IAMAP(I).EQ.2 .AND. IAN(I).GT.0) IAN(I) = -1
      WRITE (IOUT,9200)
      GO TO 2000
C
C     STEP 9: COMPLETED BSSE FOR A.
C     *****************************
C
C     SAVE TOTAL ENERGY OF A, BOTH INITIAL AND FINAL.
C
  900 ZEN(9,1) = EDUM(1)
      ZEN(9,2) = E3
C
C     SET UP FOR MOLECULE B (CHANGE MOLECULE A ATOMS TO FLOATING ATOMS).
C
      ICHARG = ICHSAV(2)
      MULTIP = IMUSAV(2)
      NAE = NAESAV(2)
      NBE = NBESAV(2)
      NE = NESAV(2)
      DO 910 I=1,NATOMS
      IAN(I) = IANSAV(I)
  910 IF (IAMAP(I).EQ.1 .AND. IAN(I).GT.0) IAN(I) = -1
      IF (IOP(29) .EQ. 2) THEN
         IOP(4) = 11
         GO TO 1110
      END IF
      WRITE (IOUT,9210)
      GO TO 2000
C
C     STEP 10: COMPLETED BSSE FOR CT(B): OCC B + VAC A.
C     *************************************************
C
C     SAVE TOTAL ENERGY OF B, BOTH INITIAL AND FINAL.
C
 1000 ZEN(10,1) = EDUM(1)
      ZEN(10,2) = E3
      WRITE (IOUT,9220)
      GO TO 2000
C
C     STEP 11: COMPLETED BSSE FOR EX(B): OCC B + OCC A.
C     *************************************************
C
C     SAVE TOTAL ENERGY OF B, BOTH INITIAL AND FINAL.
C
 1100 ZEN(11,1) = EDUM(1)
      ZEN(11,2) = E3
 1110 WRITE (IOUT,9230)
      GO TO 2000
C
C     STEP 12: COMPLETED BSSE FOR B.
C     ******************************
C
C     SAVE TOTAL ENERGY OF B, BOTH INITIAL AND FINAL.
C     PRINT SUMMARY TABLE(S).
C
 1200 ZEN(12,1) = EDUM(1)
      ZEN(12,2) = E3
C
C     RECOVER FUNDAMENTAL CONSTANTS - DO TABLE IN KJ/MOL FIRST.
C
 1210 CALL TREAD (49, FUNCON, NFCON, 1, NFCON, 1, 0)
      CONV = FUNCON(11)
      IPASS = 0
      LABEL = '(KJ/MOL)'
      NLABEL = 7
      IF (CONV .EQ. ZERO) THEN
         CONV = TEN3
         IPASS = 1
         LABEL = '(MILLIHARTREE)'
         NLABEL = 13
      END IF
      WRITE (IOUT,9100) ZEN(2,2), ZEN(3,2), ZEN(4,2)
      IF (IOP(29).EQ.2 .OR. IOP(29).EQ. 3)
     1 WRITE (IOUT,9270) ZEN(9,2), ZEN(12,2)
C
C     GET ENERGY TERMS.
C
 1220 EAB = ZEN(2,2) + ZEN(3,2)
      DELTA = (ZEN(4,2)-EAB) * CONV
C
C     ENERGY DECOMPOSITION (NO CPC).
C
      ES = (ZEN(6,1)-EAB) * CONV
      EP = (ZEN(6,2)-ZEN(6,1)) * CONV
      EX = (ZEN(4,1)-ZEN(6,1)) * CONV
      CT = (ZEN(5,2)-ZEN(5,1)) * CONV
      XIX = DELTA - ES - EP - EX - CT
C
C     ENERGY DECOMPOSITION WITH CPC.
C
      CTC = CT - (ZEN(7,2)+ZEN(10,2)-EAB)*CONV
      EXC = EX - (ZEN(8,2)+ZEN(11,2)-EAB)*CONV
      DELTAC = DELTA - (ZEN(9,2)+ZEN(12,2)-EAB)*CONV
      XIXC = DELTAC - (ES+EP+EXC+CTC)
      EXCA = (-ZEN(8,2)+ZEN(2,2)) * CONV
      CTCA = (-ZEN(7,2)+ZEN(2,2)) * CONV
      TCA = (-ZEN(9,2)+ZEN(2,2)) * CONV
      XIXCA = TCA - EXCA - CTCA
      EXCB = (-ZEN(11,2)+ZEN(3,2)) * CONV
      CTCB = (-ZEN(10,2)+ZEN(3,2)) * CONV
      TCB = (-ZEN(12,2)+ZEN(3,2)) * CONV
      XIXCB = TCB - EXCB - CTCB
C
      WRITE (IOUT,9110) DELTA, LABEL(2:NLABEL)
      IF (IOP(29).EQ.2 .OR. IOP(29).EQ. 3)
     1 WRITE (IOUT,9240) DELTAC, LABEL(2:NLABEL)
      IF (IOP(29).EQ.1 .OR. IOP(29).EQ. 3)
     1 WRITE (IOUT,9120) LABEL, ES, EP, EX, CT, XIX
      IF (IOP(29) .EQ. 3) THEN
         WRITE (IOUT,9250) LABEL, EXC, CTC, XIXC
         WRITE (IOUT,9260) LABEL, EXCA, CTCA, TCA, XIXCA,
     1    EXCB, CTCB, TCB, XIXCB
      END IF
C
C     REPEAT TABLE IN KCAL/MOL (IF CONVERSION FACTOR IS DEFINED).
C
      IF (IPASS .NE. 0) GO TO 1230
      CONV = FUNCON(10)
      IPASS = 1
      LABEL = '(KCAL/MOL)'
      NLABEL = 9
      GO TO 1220
C
C     TELL CHAIN ROUTINE THAT THE ED/BSSE IS COMPLETE.
C
 1230 IOP(1) = 1
C
C     ED / BSSE EXIT.
C     ***************
C
C     REWRITE MAP BETWEEN BASIS FUNCTIONS AND MOLECULES.
C     INCREMENT IOP(4), WHICH CONTROLS THE ED/BSSE PROCESSING.
C
 2000 CALL TWRITE (44, AIBMAP, NFILE(44,1), 1, NFILE(44,1), 1, 0)
      IOP(4) = IOP(4) + 1
      RETURN
      END
