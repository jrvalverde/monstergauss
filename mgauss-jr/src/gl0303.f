C     GL0303       05 JUN 88                                         MRP
C?IBM/GLD/GBR/VAX/UNX
      SUBROUTINE DIPOLE
C??
C?CDC
C     PROGRAM DIPOLE
C??
C1INTRODUCTION
C     LINK 0303
C*
C     ----------------
C     GAUSSIAN 82
C     U OF T VERSION
C     JULY 1987
C     ----------------
C*
C     CALCULATION OF X, Y AND Z DIPOLE MOMENT INTEGRALS.
C
C1OPTIONS
C     ******************************************************************
C     OPTIONS ... IOP() ... SEE PROGRAM GINPUT (LINK 301).
C     ******************************************************************
C*
C/
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C#
C     PARAMETER (NA=#NA, NB=#NB)
C     PARAMETER (NS=#NS, NP=#NP)
C##
      PARAMETER (NA= 36, NB=200)
      PARAMETER (NS=120, NP=300)
C###
      PARAMETER (NBB=NB*(NB+1)/2)
      PARAMETER (ZERO=0.0D0, ONE=1.0D0)
C
      INTEGER SHELLA,SHELLN,SHELLT,SHELLC,AOS
C
      COMMON /A/ IOP(99)
      COMMON /A/ NATOMS,ICHARG,MULTIP,IAN(NA),NAE,NBE,NE,NBASIS,C(NA,3)
      COMMON /A/ CDUM(4),ICDUM(401)
C
      COMMON/B/IXYZ(NS),SHELLA(NS),SHELLN(NS),SHELLT(NS),
     1 SHELLC(NS),AOS(NS),NSHELL,MAXTYP,EXX(NP),C1(NP),C2(NP)
      COMMON/BUF/DX(NBB),DY(NBB)
      COMMON/LIMIT/IMJ,ISTART,JSTART,IEND,JEND,IRANGE,JRANGE,
     1 ITYPE,JTYPE,IAOS,JAOS,IOP8,JOP8,LIND(NB)
      COMMON/C303A/INDIX(20),INDIY(20),INDIZ(20),CA(20),CB(20),SX(32),
     1 SY(32),SZ(32),DDX(100),DDY(100),DDZ(100),S1C(6),CCX(192),
     2 CCY(192),CCZ(192),DZ(NBB)
      COMMON /I1ECOM/ LAMAX, LBMAX, LPMAX
      COMMON/IO/IN,IOUT,IODUM(215)
      COMMON/GEN/E1,E2,E3,EDUM(3),DCONV,SPIN(2),FZ(3),SCR(3),DPOL(4),VCM
C
      DIMENSION INDJX(20),INDJY(20),INDJZ(20),IJST(4),IJEND(4)
      DIMENSION DD(NB,NB)
C
      EQUIVALENCE (DD(1,1),DX(1))
C
      DATA INDJX/1,2,1,1,3,1,1,2,2,1,4,1,1,2,3,3,2,1,1,2/
      DATA INDJY/1,1,2,1,1,3,1,2,1,2,1,4,1,3,2,1,1,2,3,2/
      DATA INDJZ/1,1,1,2,1,1,3,1,2,2,1,1,4,1,1,2,3,3,2,2/
      DATA IJST/1,1,5,11/, IJEND/1,4,10,20/
      DATA PI/3.14159265358979D0/
      DATA CUT1/-600.0D0/, PT5/0.5D0/
C
 2000 FORMAT('1X INTEGRALS')
 2010 FORMAT('1Y INTEGRALS')
 2020 FORMAT('1Z INTEGRALS')
C
      IF(IOP(10).EQ.0)RETURN
C     FOR ED/BSSE, IS THIS LINK REQUIRED FOR THIS PASS.
      IF (IOP(4).EQ.1 .OR. IOP(4).EQ.5 .OR. IOP(4).EQ.6 .OR.
     1 IOP(4).EQ.8 .OR. IOP(4).EQ.11) RETURN
      IF ((IOP(4).EQ.9 .OR. IOP(4).EQ.12) .AND. IOP(34).EQ.3) RETURN
      CALL FILLST
      CALL PURSET
      NTT=(NBASIS*(NBASIS+1))/2
      IOP8 = IOP(8)
      JOP8 = IOP(8)
      DO 30 I=1,20
      INDIX(I)=4*(INDJX(I)-1)
      INDIY(I)=4*(INDJY(I)-1)
   30 INDIZ(I)=4*(INDJZ(I)-1)
C     FILL INDEXING ARRAY FOR FILMAT.
      DO 60 I=1,NBASIS
   60 LIND(I)=(I*(I-1))/2
C
C     LOOP OVER SHELLS.
C
C     LOOP OVER ISHELL.
C
      DO 1000 ISHELL=1,NSHELL
      I=IXYZ(ISHELL)
      XA=C(I,1)
      YA=C(I,2)
      ZA=C(I,3)
      IGBEGN=SHELLA(ISHELL)
      IGEND=IGBEGN+SHELLN(ISHELL)-1
      ITYPE=SHELLT(ISHELL)
      LAMAX=ITYPE+1
      I=IJST(LAMAX)
      ISTART=I+SHELLC(ISHELL)
      IEND=IJEND(LAMAX)
      IRANGE=IEND-ISTART+1
      IAOS=AOS(ISHELL)-I
C
C     LOOP OVER JSHELL.
C
      DO 1000 JSHELL=1,ISHELL
      J=IXYZ(JSHELL)
      XB=C(J,1)
      YB=C(J,2)
      ZB=C(J,3)
      JGBEGN=SHELLA(JSHELL)
      JGEND=JGBEGN+SHELLN(JSHELL)-1
      JTYPE=SHELLT(JSHELL)
      LBMAX=JTYPE+1
      J=IJST(LBMAX)
      JSTART=J+SHELLC(JSHELL)
      JEND=IJEND(LBMAX)
      JRANGE=JEND-JSTART+1
      JAOS=AOS(JSHELL)-J
C
      LPMAX=LAMAX+JTYPE
      LIM1DS=(LPMAX+2)/2
      LENTQ=IRANGE*JRANGE
      IMJ=ISHELL-JSHELL
      ABX=XB-XA
      ABY=YB-YA
      ABZ=ZB-ZA
      RABSQ=ABX*ABX+ABY*ABY+ABZ*ABZ
      DO 90 I=1,LENTQ
      DDX(I)=ZERO
      DDY(I)=ZERO
   90 DDZ(I)=ZERO
C
C     LOOP OVER PRIMITIVE GAUSSIANS.
C
      DO 900 IGAUSS=IGBEGN,IGEND
      AS=EXX(IGAUSS)
      ASXA=AS*XA
      ASYA=AS*YA
      ASZA=AS*ZA
      ARABSQ=AS*RABSQ
      CALL FILLC(ITYPE,IGAUSS,C1,C2,CA)
C
      DO 900 JGAUSS=JGBEGN,JGEND
      BS=EXX(JGAUSS)
      CALL FILLC(JTYPE,JGAUSS,C1,C2,CB)
C
      EP=AS+BS
      EPI=ONE/EP
      EPIO2=PT5*EPI
      ARGP=-BS*ARABSQ*EPI
      PEXP=ZERO
      IF(ARGP.GT.CUT1)PEXP=DEXP(ARGP)
      PX=(ASXA+BS*XB)*EPI
      PY=(ASYA+BS*YB)*EPI
      PZ=(ASZA+BS*ZB)*EPI
      XAP=PX-XA
      YAP=PY-YA
      ZAP=PZ-ZA
      XBP=PX-XB
      YBP=PY-YB
      ZBP=PZ-ZB
      CALL GETCC1(CCX,XAP,XBP,1)
      CALL GETCC1(CCY,YAP,YBP,1)
      CALL GETCC1(CCZ,ZAP,ZBP,1)
      STERM=DSQRT(EPI*PI)
      CALL GET1CS(S1C,STERM,EPIO2,1)
      CALL GET2CS(SX,S1C,CCX,1)
      CALL GET2CS(SY,S1C,CCY,1)
      DO 200 I=1,LIM1DS
  200 S1C(I)=S1C(I)*PEXP
      CALL GET2CS(SZ,S1C,CCZ,1)
C
C     BEGIN LOOP OVER ATOMIC ORBITALS.
C
      INTC=0
      DO 210 I=ISTART,IEND
      IX=INDIX(I)
      IY=INDIY(I)
      IZ=INDIZ(I)
      IXNEW=IX+4
      IYNEW=IY+4
      IZNEW=IZ+4
      DO 210 J=JSTART,JEND
      JX=INDJX(J)
      JY=INDJY(J)
      JZ=INDJZ(J)
      INTC=INTC+1
C
      COEF=CA(I)*CB(J)
      DDX(INTC)=DDX(INTC)+COEF*SY(IY+JY)*SZ(IZ+JZ)*(XA*SX(IX+JX)+
     1 SX(IXNEW+JX))
      DDY(INTC)=DDY(INTC)+COEF*SX(IX+JX)*SZ(IZ+JZ)*(YA*SY(IY+JY)+
     1 SY(IYNEW+JY))
      DDZ(INTC)=DDZ(INTC)+COEF*SX(IX+JX)*SY(IY+JY)*(ZA*SZ(IZ+JZ)+
     1 SZ(IZNEW+JZ))
  210 CONTINUE
C
  900 CONTINUE
C     END OF LOOP OVER GAUSSIANS.
C
C     FILMAT TAKES THE INTEGRALS IN DDX, DDY, AND DDZ AND STORES THEM
C     IN THE PROPER PLACES IN DX, DY AND DZ.
C
      CALL FILMAT(DDX,DX,NBB)
      CALL FILMAT(DDY,DY,NBB)
      CALL FILMAT(DDZ,DZ,NBB)
C
 1000 CONTINUE
C     END OF LOOP OVER SHELLS.
C
      CALL TWRITE(34,DX,NBB,1,NTT,1,0)
      CALL TWRITE(35,DY,NBB,1,NTT,1,0)
      CALL TWRITE(36,DZ,NBB,1,NTT,1,0)
C
      IF(IOP(17).EQ.0)RETURN
      CALL TREAD(34,DD,NB,NB,NBASIS,NBASIS,1)
      WRITE(IOUT,2000)
      CALL GBSOUT(DD,DX,NB,NB,NBASIS,0)
      CALL TREAD(35,DD,NB,NB,NBASIS,NBASIS,1)
      WRITE(IOUT,2010)
      CALL GBSOUT(DD,DX,NB,NB,NBASIS,0)
      CALL TREAD(36,DD,NB,NB,NBASIS,NBASIS,1)
      WRITE(IOUT,2020)
      CALL GBSOUT(DD,DX,NB,NB,NBASIS,0)
      RETURN
      END
